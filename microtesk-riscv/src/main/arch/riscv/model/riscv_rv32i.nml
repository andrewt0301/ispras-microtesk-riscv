/*
 * Copyright 2017 ISP RAS (http://www.ispras.ru)
 * 
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

#ifndef RISCV_RV32I_INCLUDED
#define RISCV_RV32I_INCLUDED

//==================================================================================================
// RV32I Base Instruction Set
//==================================================================================================

/*
  Instructions (number: 47):
    LUI +
    AUIPC +
    JAL +
    JALR +
    BEQ +
    BNE +
    BLT +
    BGE +
    BLTU +
    BGEU +
    LB +
    LH +
    LW +
    LBU +
    LHU +
    SB +
    SH +
    SW +
    ADDI +
    SLTI +
    SLTIU +
    XORI +
    ORI +
    ANDI +
    SLLI +
    SRLI +
    SRAI +
    SLL +
    SRL +
    SRA +
    ADD +
    SUB +
    SLT +
    SLTU +
    XOR +
    OR +
    AND +
    FENCE
    FENCE.I
    ECALL
    EBREAK
    CSRRW
    CSRRS
    CSRRC
    CSRRWI
    CSRRSI
    CSRRCI

  Pseudoinstructions (number: 16):
    NOP +
    MV +
    NOT +
    NEG +
    SEQZ +
    SNEZ +
    SLTZ +
    SGTZ +
    J +
    JR +
    BEQZ +
    BNEZ +
    BLEZ +
    BGEZ +
    BLTZ +
    BGTZ +
*/

//==================================================================================================
// Instructions
//==================================================================================================

/*
  LUI : Load Upper Immediate (U-type)

  Format: lui rd, imm

  RV32I:

  LUI (load upper immediate) is used to build 32-bit constants and uses the U-type
  format. LUI places the U-immediate value in the top 20 bits of the destination
  register rd, filling in the lowest 12 bits with zeros.

  RV64I:

  LUI (load upper immediate) uses the same opcode as RV32I. LUI places the 20-bit
  U-immediate into bits 31–12 of register rd and places zero in the lowest 12 bits.
  The 32-bit result is sign-extended to 64 bits.
*/
op lui(rd: X, imm: card(20))
  syntax = format("lui %s, 0x%x", rd.syntax, imm)
  image  = format("%20s%s0110111", imm, rd.image)
  action = {
    rd = sign_extend(XWORD, imm) << 12;
  }

/*
  AUIPC : Add Upper Immediate to PC (U-type)

  Format: auipc rd, imm

  RV32I:

  AUIPC (add upper immediate to pc) is used to build pc-relative addresses and uses
  the U-type format. AUIPC forms a 32-bit offset from the 20-bit U-immediate, filing
  in the lowest 12 bits with zeros, adds this offset to the pc, then places the result
  in register rd.

  RV64I:

  AUIPC (add upper immediate to pc) uses the same opcode as RV32I. AUIPC is used to
  build pc-relative addresses and uses the U-type format. AUIPC appends 12 low-order
  zero bits to the 20-bit U-immediate, sign-extends the result to 64 bits, then adds
  it to the pc and places the result in register rd.
*/
op auipc(rd: X, imm: card(20))
  syntax = format("auipc %s, 0x%x", rd.syntax, imm)
  image  = format("%20s%s0010111", imm, rd.image)
  action = {
    rd = PC + (sign_extend(XWORD, imm) << 12);
  }

/*
  JAL: Jump and Link (UJ-type)

  Format: jal rd, imm

  The jump and link (JAL) instruction uses the UJ-type format, where the J-immediate
  encodes a signed offset in multiples of 2 bytes. The offset is sign-extended and added
  to the pc to form the jump target address. Jumps can therefore target a +/- 1MiB range.
  JAL stores the address of the instruction following the jump (pc+4) into register rd.
  The standard software calling convention uses x1 as the return address register.

  The JAL instruction can generate a misaligned instruction fetch exception if the target
  address is not aligned to a four-byte boundary.
*/
op jal(rd: X, imm: card(20))
  syntax = format("jal %s, 0x%x", rd.syntax, imm)
  image  = format("%1s%10s%1s%8s%s1101111", imm<19>, imm<9..0>, imm<10>, imm<18..11>, rd.image)
  action = {
    rd = PC + 4;
    PC = PC + (sign_extend(XWORD, imm) << 1);
  }

/*
  JALR: Jump and Link Register (I-type)

  Format: jalr rd, rs1, imm

  The indirect jump instruction JALR (jump and link register) uses the I-type encoding.
  The target address is obtained by adding the 12-bit signed I-immediate to the register rs1,
  then setting the least-significant bit of the result to zero. The address of the instruction
  following the jump (pc+4) is written to register rd. Register x0 can be used as the destination
  if the result is not required.

  The JALR instructions can generate a misaligned instruction fetch exception if the target
  address is not aligned to a four-byte boundary.
*/
op jalr(rd: X, rs1: X, imm: card(12))
  syntax = format("jalr %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s000%s1100111", imm, rs1.image, rd.image)
  action = {
    rd = PC + 4;
    PC = rs1 + ((sign_extend(XWORD, imm) << 1) >> 1);
  }

/*
  BEQ: Branch Equal (SB-type)
  BNE: Branch Not Equal (SB-type)

  Format: beq rs1, rs2, imm
          bne rs1, rs2, imm

  All branch instructions use the SB-type instruction format. The 12-bit B-immediate
  encodes signed offsets in multiples of 2, and is added to the current pc to give the
  target address. The conditional branch range is +/- 4 KiB. Branch instructions compare
  two registers.

  BEQ and BNE take the branch if registers rs1 and rs2 are equal or unequal respectively.
*/
op beq(rs1: X, rs2: X, imm: card(12))
  syntax = format("beq %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s000%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if rs1 == rs2 then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

op bne(rs1: X, rs2: X, imm: card(12))
  syntax = format("bne %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s001%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if rs1 != rs2 then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

/*
  BLT: Branch Less Than (SB-type)
  BLTU: Branch Less Than Unsigned (SB-type)

  Format: blt rs1, rs2, imm
          bltu rs1, rs2, imm

  All branch instructions use the SB-type instruction format. The 12-bit B-immediate
  encodes signed offsets in multiples of 2, and is added to the current pc to give the
  target address. The conditional branch range is +/- 4 KiB. Branch instructions compare
  two registers.

  BLT and BLTU take the branch if rs1 is less than rs2, using signed and unsigned
  comparison respectively.
*/
op blt(rs1: X, rs2: X, imm: card(12))
  syntax = format("blt %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s100%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if cast(XINT, rs1) < cast(XINT, rs2) then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

op bltu(rs1: X, rs2: X, imm: card(12))
  syntax = format("bltu %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s110%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if rs1 < rs2 then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

/*
  BGE: Branch Greater Equal (SB-type)
  BGEU: Branch Greater Equal Unsigned (SB-type)

  Format: bge rs1, rs2, imm
          bgeu rs1, rs2, imm

  All branch instructions use the SB-type instruction format. The 12-bit B-immediate
  encodes signed offsets in multiples of 2, and is added to the current pc to give the
  target address. The conditional branch range is +/- 4 KiB. Branch instructions compare
  two registers.

  BGE and BGEU take the branch if rs1 is greater than or equal to rs2, using signed
  and unsigned comparison respectively.
*/
op bge(rs1: X, rs2: X, imm: card(12))
  syntax = format("bge %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s101%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if cast(XINT, rs1) >= cast(XINT, rs2) then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

op bgeu(rs1: X, rs2: X, imm: card(12))
  syntax = format("bgeu %s, %s, 0x%x", rs1.syntax, rs2.syntax, imm)
  image  = format("%1s%6s%s%s111%4s%1s1100011",
                  imm<11>, imm<9..4>, rs2.image, rs1.image, imm<3..0>, imm<10>)
  action = {
    if rs1 >= rs2 then
      PC = PC + (sign_extend(XWORD, imm) << 1);
    endif;
  }

/*
  LW:  Load Word (I-type)
  LH:  Load Half (I-type)
  LHU: Load Half Unsigned (I-type)
  LB:  Load Byte (I-type)
  LBU: Load Half Unsigned (I-type)

  Format: lw  rd, rs1, imm
  Format: lh  rd, rs1, imm
  Format: lhu rd, rs1, imm
  Format: lb  rd, rs1, imm
  Format: lbu rd, rs1, imm

  LW loads a 32-bit value from memory into rd.
  LH loads a 16-bit value from memory, then sign-extends to 32-bits before storing in rd.
  LHU loads a 16-bit value from memory but then zero extends to 32-bits before storing in rd.
  LB and LBU are deﬁned analogously for 8-bit values.

  Load instructions transfer a value between the registers and memory.
  Loads are encoded in the I-type format. The effective byte address is obtained by adding
  register rs1 to the sign-extended 12-bit offset. Loads copy a value from memory to
  register rd.

  For best performance, the effective address for all loads and stores should be
  naturally aligned for each data type (i.e., on a four-byte boundary for 32-bit accesses,
  and a two-byte boundary for 16-bit accesses). The base ISA supports misaligned accesses,
  but these might run extremely slowly depending on the implementation. Furthermore,
  naturally aligned loads and stores are guaranteed to execute atomically, whereas misaligned
  loads and stores might not, and hence require additional synchronization to ensure atomicity.
*/

op lw(rd: X, rs1: X, imm: card(12))
  syntax = format("lw %s, 0x%x(%s)", rd.syntax, imm, rs1.syntax)
  image  = format("%12s%s010%s0000011", imm, rs1.image, rd.image)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    if tmp_address<1..0> == 0 then // Word-aligned address
      tmp_word = MEM[tmp_address >> 2];
    elif tmp_address<1..0> == 1 then
      tmp_word = MEM[tmp_address >> 2 + 1]<7..0> :: MEM[tmp_address >> 2]<31..8>;
    elif tmp_address<1..0> == 2 then
      tmp_word = MEM[tmp_address >> 2 + 1]<15..0> :: MEM[tmp_address >> 2]<31..16>;
    else
      tmp_word = MEM[tmp_address >> 2 + 1]<23..0> :: MEM[tmp_address >> 2]<31..24>;
    endif;

    rd = sign_extend(XWORD, tmp_word);
  }

op lh(rd: X, rs1: X, imm: card(12))
  syntax = format("lh %s, 0x%x(%s)", rd.syntax, imm, rs1.syntax)
  image  = format("%12s%5s001%5s0000011", imm, rs1.image, rd.image)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    if tmp_address<1..0> != 3 then
      tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
      tmp_half = MEM[tmp_address >> 2]<tmp_bit_offset + 15..tmp_bit_offset>;
    else
      tmp_half = MEM[(tmp_address >> 2) + 1]<7..0> :: MEM[tmp_address >> 2]<31..24>;
    endif;

    rd = sign_extend(XWORD, tmp_half);
  }

op lhu(rd: X, rs1: X, imm: card(12))
  syntax = format("lhu %s, 0x%x(%s)", rd.syntax, imm, rs1.syntax)
  image  = format("%12s%s101%s0000011", imm, rs1.image, rd.image)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    if tmp_address<1..0> != 3 then
      tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
      tmp_half = MEM[tmp_address >> 2]<tmp_bit_offset + 15..tmp_bit_offset>;
    else
      tmp_half = MEM[(tmp_address >> 2) + 1]<7..0> :: MEM[tmp_address >> 2]<31..24>;
    endif;

    rd = zero_extend(XWORD, tmp_half);
  }

op lb(rd: X, rs1: X, imm: card(12))
  syntax = format("lb %s, 0x%x(%s)", rd.syntax, imm, rs1.syntax)
  image  = format("%12s%s000%s0000011", imm, rs1.image, rd.image)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
    tmp_byte = MEM[tmp_address >> 2]<tmp_bit_offset + 7..tmp_bit_offset>;

    rd = sign_extend(XWORD, tmp_byte);
  }

op lbu(rd: X, rs1: X, imm: card(12))
  syntax = format("lbu %s, 0x%x(%s)", rd.syntax, imm, rs1.syntax)
  image  = format("%12s%s100%s0000011", imm, rs1.image, rd.image)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
    tmp_byte = MEM[tmp_address >> 2]<tmp_bit_offset + 7..tmp_bit_offset>;

    rd = zero_extend(XWORD, tmp_byte);
  }

/*
  SW: Store Word (S-type)
  SH: Store Half (S-type)
  SB: Store Byte (S-type)

  Format: sw rs1, rs2, imm
  Format: sh rs1, rs2, imm
  Format: sb rs1, rs2, imm

  The SW, SH, and SB instructions store 32-bit, 16-bit, and 8-bit values from
  the low bits of register rs2 to memory.
*/

op sw(rs2: X, rs1: X, imm: card(12))
  syntax = format("sw %s, 0x%x(%s)", rs2.syntax, imm, rs1.syntax)
  image  = format("%7s%s%s010%5s0100011", imm<11..5>, rs2.image, rs1.image, imm<4..0>)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    if tmp_address<1..0> == 0 then // Word-aligned address
      MEM[coerce(card(MEM_SIZE), tmp_address >> 2)] = rs2<31..0>;
    elif tmp_address<1..0> == 1 then
      MEM[coerce(card(MEM_SIZE), (tmp_address >> 2) + 1)]<7..0> :: MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<31..8> = rs2<31..0>;
    elif tmp_address<1..0> == 2 then
      MEM[coerce(card(MEM_SIZE), (tmp_address >> 2) + 1)]<15..0> :: MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<31..16> = rs2<31..0>;
    else
      MEM[coerce(card(MEM_SIZE), (tmp_address >> 2) + 1)]<23..0> :: MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<31..24> = rs2<31..0>;
    endif;
  }

op sh(rs2: X, rs1: X, imm: card(12))
  syntax = format("sh %s, 0x%x(%s)", rs2.syntax, imm, rs1.syntax)
  image  = format("%7s%s%s001%5s0100011", imm<11..5>, rs2.image, rs1.image, imm<4..0>)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);

    if tmp_address<1..0> != 3 then
      tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
      MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<tmp_bit_offset + 15..tmp_bit_offset> = rs2<15..0>;
    else
      MEM[coerce(card(MEM_SIZE), (tmp_address >> 2) + 1)]<7..0> :: MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<31..24> = rs2<15..0>;
    endif;
  }

op sb(rs2: X, rs1: X, imm: card(12))
  syntax = format("sb %s, 0x%x(%s)", rs2.syntax, imm, rs1.syntax)
  image  = format("%7s%5s%5s000%5s0100011", imm<11..5>, rs2.image, rs1.image, imm<4..0>)
  action = {
    tmp_address = rs1 + sign_extend(XWORD, imm);
    tmp_bit_offset = zero_extend(card(5), tmp_address<1..0>) * 8;
    MEM[coerce(card(MEM_SIZE), tmp_address >> 2)]<(tmp_bit_offset + 7)..tmp_bit_offset> = rs2<7..0>;
  }

op rv32i_mmu = lb
             | lh
             | lw
             | lbu
             | lhu
             | sb
             | sh
             | sw

/*
  ADDI: Add Immediate (I-type)

  Format: addi rd, rs1, imm

  ADDI adds the sign-extended 12-bit immediate to register rs1. Arithmetic overﬂow is
  ignored and the result is simply the low XLEN bits of the result. ADDI rd, rs1, 0
  is used to implement the MV rd, rs1 assembler pseudo-instruction.
*/
op addi(rd: X, rs1: X, imm: card(12))
  syntax = format("addi %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s000%s0010011", imm, rs1.image, rd.image)
  action = {
    rd = rs1 + sign_extend(XWORD, imm);
  }

/*
  SLTI: Set Less Than Immediate (I-type)

  Format: slti rd, rs1, imm

  SLTI (set less than immediate) places the value 1 in register rd if register rs1 is
  less than the sign-extended immediate when both are treated as signed numbers,
  else 0 is written to rd.
*/
op slti(rd: X, rs1: X, imm: card(12))
  syntax = format("slti %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s010%s0010011", imm, rs1.image, rd.image)
  action = {
    if cast(XINT, rs1) < sign_extend(XINT, imm) then
      rd = 1;
    else
      rd = 0;
    endif;
  }

/*
  SLTIU: Set Less Than Immediate Unsigned (I-type)

  Format: sltiu rd, rs1, imm

  SLTIU is similar to SLTI but compares the values as unsigned numbers (i.e.,
  the immediate is first sign-extended to XLEN bits then treated as an unsigned number).
  Note, SLTIU rd, rs1, 1 sets rd to 1 if rs1 equals zero, otherwise sets rd to 0
  (assembler pseudo-op SEQZ rd, rs).
*/
op sltiu(rd: X, rs1: X, imm: card(12))
  syntax = format("sltiu %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s011%s0010011", imm, rs1.image, rd.image)
  action = {
    if rs1 < sign_extend(XWORD, imm) then
      rd = 1;
    else
      rd = 0;
    endif;
  }

/*
  XORI: XOR Immediate (I-type)

  Format: xori rd, rs1, imm

  XORI is a logical operation that performs bitwise XOR on register rs1 and
  the sign-extended 12-bit immediate and place the result in rd.
  Note, XORI rd, rs1, -1 performs a bitwise logical inversion of register rs1
  (assembler pseudo-instruction NOT rd, rs).
*/
op xori(rd: X, rs1: X, imm: card(12))
  syntax = format("xori %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s100%s0010011", imm, rs1.image, rd.image)
  action = {
    rd = rs1 ^ sign_extend(XWORD, imm);
  }

/*
  ORI: OR Immediate (I-type)

  Format: ori rd, rs1, imm

  ORI is a logical operation that performs bitwise OR on register rs1
  and the sign-extended 12-bit immediate and place the result in rd.
*/
op ori(rd: X, rs1: X, imm: card(12))
  syntax = format("ori %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s110%s0010011", imm, rs1.image, rd.image)
  action = {
    rd = rs1 | sign_extend(XWORD, imm);
  }

/*
  ANDI: AND Immediate (I-type)

  Format: andi rd, rs1, imm

  ANDI is a logical operation that performs bitwise AND on register rs1
  and the sign-extended 12-bit immediate and place the result in rd.
*/
op andi(rd: X, rs1: X, imm: card(12))
  syntax = format("andi %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("%12s%s111%s0010011", imm, rs1.image, rd.image)
  action = {
    rd = rs1 & sign_extend(XWORD, imm);
  }

/*
  SLLI: Shift Left Logical Immediate (I-type)
  SRLI: Shift Right Logical Immediate (I-type)
  SRAI: Shift Right Arithmetic Immediate (I-type)

  Format: slli rd, rs1, imm
  Format: srli rd, rs1, imm
  Format: srai rd, rs1, imm

  SLLI is a logical left shift (zeros are shifted into the lower bits).
  SRLI is a logical right shift (zeros are shifted into the upper bits).
  SRAI is an arithmetic right shift (the original sign bit is copied into the vacated upper bits).

  RV32I:

  Shifts by a constant are encoded as a specialization of the I-type format. The operand
  to be shifted is in rs1, and the shift amount is encoded in the lower 5 bits (SHLEN = 5)
  of the I-immediate ﬁeld. The right shift type is encoded in a high bit of the I-immediate.

  RV64I:

  Shifts by a constant are encoded as a specialization of the I-type format using the same
  instruction opcode as RV32I. The operand to be shifted is in rs1, and the shift amount
  is encoded in the lower 6 bits (SHLEN = 6) of the I-immediate ﬁeld for RV64I. The right
  shift type is  encoded in bit 30. For RV32I, SLLI, SRLI, and SRAI generate an illegal
  instruction exception if imm[5] = 0.
*/
op slli(rd: X, rs1: X, imm: card(SHLEN))
  syntax = format("slli %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("000000%1s%5s%s001%s0010011",
                  if SHLEN == 6 then imm<5> else coerce(BIT, 0) endif,
                  if SHLEN == 5 then imm else imm<4..0> endif,
                  rs1.image,
                  rd.image
                  )
  action = {
    rd = rs1 << imm;
  }

op srli(rd: X, rs1: X, imm: card(SHLEN))
  syntax = format("srli %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("000000%1s%5s%s101%s0010011",
                   if SHLEN == 6 then imm<5> else coerce(BIT, 0) endif,
                   if SHLEN == 5 then imm else imm<4..0> endif,
                   rs1.image,
                   rd.image
                   )
  action = {
    rd = rs1 >> imm;
  }

op srai(rd: X, rs1: X, imm: card(SHLEN))
  syntax = format("srai %s, %s, 0x%x", rd.syntax, rs1.syntax, imm)
  image  = format("010000%1s%5s%s101%s0010011",
                  if SHLEN == 6 then imm<5> else coerce(BIT, 0) endif,
                  if SHLEN == 5 then imm else imm<4..0> endif,
                  rs1.image,
                  rd.image
                  )
  action = {
    rd = cast(XINT, rs1) >> imm;
  }

/*
  SLL: Shift Left Logical (R-type)
  SRL: Shift Right Logical (R-type)
  SRA: Shift Right Arithmetic (R-type)

  Format: sll rd, rs1, rs2
          srl rd, rs1, rs2
          sra rd, rs1, rs2

  SLL, SRL, and SRA perform logical left, logical right, and arithmetic right shifts on
  the value in register rs1 by the shift amount held in register rs2.

  RV32I: The shift amount is held in the lower 5 bits of register rs2 (SHLEN = 5).
  RV64I: Only the low 6 bits of rs2 are considered for the shift amount (SHLEN = 6).
*/
op sll(rd: X, rs1: X, rs2: X)
  syntax = format("sll %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s001%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 << rs2<SHLEN-1..0>;
  }

op srl(rd: X, rs1: X, rs2: X)
  syntax = format("srl %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s101%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 >> rs2<SHLEN-1..0>;
  }

op sra(rd: X, rs1: X, rs2: X)
  syntax = format("sra %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0100000%s%s101%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = cast(XINT, rs1) >> rs2<SHLEN-1..0>;
  }

/*
  ADD and SUB (R-type)

  Format: add rd, rs1, rs2
          sub rd, rs1, rs2

  ADD and SUB perform addition and subtraction respectively. Overﬂows are ignored
  and the low XLEN bits of results are written to the destination.
*/
op add(rd: X, rs1: X, rs2: X)
  syntax = format("add %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s000%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 + rs2;
  }

op sub(rd: X, rs1: X, rs2: X)
  syntax = format("sub %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0100000%s%s000%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 - rs2;
  }

/*
  SLT and SLTU: Set Less Than and Set Less Than Unsigned (R-type)

  Format: slt rd, rs1, rs2
          sltu rd, rs1, rs2

  SLT and SLTU  perform signed and unsigned compares respectively, writing 1 to
  rd if rs1 < rs2, 0 otherwise. Note, SLTU rd, x0, rs2 sets rd to 1 if rs2 is not
  equal to zero, otherwise sets rd to zero (assembler pseudo-op SNEZ rd, rs).
*/
op slt(rd: X, rs1: X, rs2: X)
  syntax = format("slt %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s010%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    if cast(XINT, rs1) < cast(XINT, rs2) then
      rd = 1;
    else
      rd = 0;
    endif;
  }

op sltu(rd: X, rs1: X, rs2: X)
  syntax = format("sltu %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s011%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    if rs1 < rs2 then
      rd = 1;
    else
      rd = 0;
    endif;
  }

/*
  AND, OR, and XOR (R-type)

  Format: and rd, rs1, rs2
          or rd, rs1, rs2
          xor rd, rs1, rs2

  AND, OR, and XOR perform bitwise logical operations.
*/
op and(rd: X, rs1: X, rs2: X)
  syntax = format("and %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s111%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 & rs2;
  }

op or(rd: X, rs1: X, rs2: X)
  syntax = format("or %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s110%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 | rs2;
  }

op xor(rd: X, rs1: X, rs2: X)
  syntax = format("xor %s, %s, %s", rd.syntax, rs1.syntax, rs2.syntax)
  image  = format("0000000%s%s100%s0110011", rs2.image, rs1.image, rd.image)
  action = {
    rd = rs1 ^ rs2;
  }

//==================================================================================================
// Pseudoinstructions
//==================================================================================================

/*
  NOP: No operation (addi x0, x0, 0)

  Format: nop

  The NOP instruction does not change any user-visible state, except for advancing
  the pc. NOP is encoded as ADDI x0, x0, 0.
*/
pseudo op nop()
  syntax = "nop"
  image  = format("%s", addi(X(0), X(0), 0).image)
  action = {
    addi(X(0), X(0), 0).action;
  }

/*
  MV: Copy register (addi rd, rs, 0)

  Format: mv rd, rs
*/
pseudo op mv(rd: X, rs: X)
  syntax = format("mv %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", addi(rs, rd, 0).image)
  action = {
    addi(rd, rs, 0).action;
  }

/*
  NOT: One's complement (xori rd, rs, -1)

  Format: not rd, rs
*/
pseudo op not(rd: X, rs: X)
  syntax = format("not %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", xori(rs, rd, -1).image)
  action = {
    xori(rs, rd, -1).action;
  }

/*
  NEG: Two's complement (sub rd, x0, rs)

  Format: neg rd, rs
*/
pseudo op neg(rd: X, rs: X)
  syntax = format("neg %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", sub(rd, X(0), rs).image)
  action = {
    sub(rd, X(0), rs).action;
  }

/*
  SEQZ: Set if = zero (sltiu rd, rs, 1)

  Format: seqz rd, rs
*/
pseudo op seqz(rd: X, rs: X)
  syntax = format("seqz %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", sltiu(rd, rs, 1).image)
  action = {
    sltiu(rd, rs, 1).action;
  }

/*
  SNEZ: Set if != zero (sltu rd, x0, rs)

  Format: snez rd, rs
*/
pseudo op snez(rd: X, rs: X)
  syntax = format("snez %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", sltu(rd, X(0), rs).image)
  action = {
    sltu(rd, X(0), rs).action;
  }

/*
  SLTZ: Set if < zero (slt rd, rs, x0)

  Format: sltz rd, rs
*/
pseudo op sltz(rd: X, rs: X)
  syntax = format("sltz %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", slt(rd, rs, X(0)).image)
  action = {
    slt(rd, rs, X(0)).action;
  }

/*
  SGTZ: Set if > zero (slt rd, x0, rs)

  Format: sgtz rd, rs
*/
pseudo op sgtz(rd: X, rs: X)
  syntax = format("sgtz %s, %s", rd.syntax, rs.syntax)
  image  = format("%s", slt(rd, X(0), rs).image)
  action = {
    slt(rd, X(0), rs).action;
  }

/*
  J: Jump (jal x0, offset)

  Format: j offset
*/
pseudo op j(offset: card(20))
  syntax = format("j 0x%x", offset)
  image  = format("%s", jal(X(0), offset).image)
  action = {
    jal(X(0), offset).action;
  }

/*
  JR: Jump Register (jalr x0, rs, 0)

  Format: jr rs
*/
pseudo op jr(rs: X)
  syntax = format("jr %s", rs.syntax)
  image  = format("%s", jalr(X(0), rs, 0).image)
  action = {
    jalr(X(0), rs, 0).action;
  }

/*
  BEQZ: Branch if = zero (beq rs, x0, offset)

  Format: beqz rs, offset
*/
pseudo op beqz(rs: X, offset: card(12))
  syntax = format("beqz %s, 0x%x", rs.syntax, offset)
  image  = format("%s", beq(rs, X(0), offset).image)
  action = {
    beq(rs, X(0), offset).action;
  }

/*
  BNEZ: Branch if != zero (bne rs, x0, offset)

  Format: bnez rs, offset
*/
pseudo op bnez(rs: X, offset: card(12))
  syntax = format("bnez %s, 0x%x", rs.syntax, offset)
  image  = format("%s", bne(rs, X(0), offset).image)
  action = {
    bne(rs, X(0), offset).action;
  }

/*
  BLEZ: Branch if <= zero (bge x0, rs, offset)

  Format: blez rs, offset
*/
pseudo op blez(rs: X, offset: card(12))
  syntax = format("blez %s, 0x%x", rs.syntax, offset)
  image  = format("%s", bge(X(0), rs, offset).image)
  action = {
    bge(X(0), rs, offset).action;
  }

/*
  BGEZ: Branch if >= zero (bge rs, x0, offset)

  Format: bgez rs, offset
*/
pseudo op bgez(rs: X, offset: card(12))
  syntax = format("bgez %s, 0x%x", rs.syntax, offset)
  image  = format("%s", bge(rs, X(0), offset).image)
  action = {
    bge(rs, X(0), offset).action;
  }

/*
  BLTZ: Branch if < zero (blt rs, x0, offset)

  Format: bltz rs, offset
*/
pseudo op bltz(rs: X, offset: card(12))
  syntax = format("bltz %s, 0x%x", rs.syntax, offset)
  image  = format("%s", blt(rs, X(0), offset).image)
  action = {
    blt(rs, X(0), offset).action;
  }

/*
  BGTZ: Branch if > zero (blt x0, rs, offset)

  Format: bgtz rs, offset
*/
pseudo op bgtz(rs: X, offset: card(12))
  syntax = format("bgtz %s, 0x%x", rs.syntax, offset)
  image  = format("%s", blt(X(0), rs, offset).image)
  action = {
    blt(X(0), rs, offset).action;
  }

//==================================================================================================
// System instructions
//==================================================================================================

/* The ECALL instruction is used to make a request to the supporting execution environment. */
op ecall()
  syntax = format("ecall")
  image  = format("%12s00000%3s00000%7s", "000000000000", "000", "1110011")
  action = {
    // TODO
  }

/* The EBREAK instruction is used by debuggers to cause control to be transferred back to
   a debugging environment. */
op ebreak()
  syntax = format("ebreak")
  image  = format("%12s00000%3s00000%7s", "000000000001", "000", "1110011")
  action = {
    // TODO
  }

op fence()//(pred: card(4), succ: card(4))
  // syntax = format("fence 0x%x, 0x%x", pred, succ) TODO:
  syntax = format("fence")
  image  = format("%4s%4s%4s00000%3s00000%7s", "0000", "0000", "0000", "000", "0001111")
  action = {
    // TODO
  }

op fencei()
  syntax = format("fence.i")
  image  = format("%12s00000%3s00000%7s", "000000000000", "001", "0001111")
  action = {
    // TODO
  }

/* CSRRW (Atomic Read/Write CSR) */
op csrrw(rd: X, csr: CSR_MODE, rs1: X)
  syntax = format("csrrw %s, %s, %s", rd.syntax, csr.syntax, rs1.syntax)
  image  = format("%12s%5s001%5s1110011", csr.image, rs1.image, rd.image)
  action = {
    if rd.i != 0 then
      rd = zero_extend(XWORD, csr);
    endif;
    csr = rs1;
  }

/* CSRRS (Atomic Read and Set Bits in CSR) */
op csrrs(rd: X, csr: CSR_MODE, rs1: X)
  syntax = format("csrrs %s, %s, %s", rd.syntax, csr.syntax, rs1.syntax)
  image  = format("%12s%5s010%5s1110011", csr.image, rs1.image, rd.image)
  action = {
    if rs1.i != 0 then
      csr = csr | rs1;
    endif;
    rd = zero_extend(XWORD, csr);
  }

/* CSRRC (Atomic Read and Clear Bits in CSR) */
op csrrc(rd: X, csr: CSR_MODE, rs1: X)
  syntax = format("csrrc %s, %s, %s", rd.syntax, csr.syntax, rs1.syntax)
  image  = format("%12s%5s011%5s1110011", csr.image, rs1.image, rd.image)
  action = {
    if rs1.i != 0 then
      csr = csr & ~rs1;
    endif;
    rd = zero_extend(XWORD, csr);
  }

/* The CSRRWI, CSRRSI, and CSRRCI variants are similar to CSRRW, CSRRS, and CSRRC re-
  spectively, except they update the CSR using an XLEN-bit value obtained by zero-extending a 5-bit
  unsigned immediate (uimm[4:0]) */
op csrrwi(rd: X, csr: CSR_MODE, imm: card(5))
  syntax = format("csrrwi %s, %s, 0x%x", rd.syntax, csr.syntax, imm)
  image  = format("%12s%5s101%5s1110011", csr.image, imm, rd.image)
  action = {
    if rd.i != 0 then
      rd = zero_extend(XWORD, csr);
    endif;
    csr = zero_extend(XWORD, imm);
  }

op csrrsi(rd: X, csr: CSR_MODE, imm: card(5))
  syntax = format("csrrsi %s, %s, 0x%x", rd.syntax, csr.syntax, imm)
  image  = format("%12s%5s110%5s1110011", csr.image, imm, rd.image)
  action = {
    if imm != 0 then
      csr = csr | zero_extend(XWORD, imm);
    endif;
    rd = zero_extend(XWORD, csr);
  }

op csrrci(rd: X, csr: CSR_MODE, imm: card(5))
  syntax = format("csrrci %s, %s, 0x%x", rd.syntax, csr.syntax, imm)
  image  = format("%12s%5s111%5s1110011", csr.image, imm, rd.image)
  action = {
    if imm != 0 then
      csr = csr & ~zero_extend(XWORD, imm);
    endif;
    rd = zero_extend(XWORD, csr);
  }

op rv32i_system = csrrw
                | csrrs
                | csrrc
                | csrrwi
                | csrrsi
                | csrrci
                | ecall
                | ebreak
                | fence
                | fencei

//==================================================================================================
// Pseudoinstructions
//==================================================================================================

// Write CSR
pseudo op csrw(csr: CSR_MODE, rs1: X)
  syntax = format("csrw %s, %s", csr.syntax, rs1.syntax)
  image  = format("%32s", csrrw(X(0), csr, rs1).image)
  action = {
    csrrw(X(0), csr, rs1).action;
  }

// Read CSR
pseudo op csrr(rd: X, csr: CSR_MODE)
  syntax = format("csrr %s, %s", rd.syntax, csr.syntax)
  image  = format("%32s", csrrs(rd, csr, X(0)).image)
  action = {
    csrrs(rd, csr, X(0)).action;
  }

// Set bits in CSR
pseudo op csrs(csr: CSR_MODE, rs1: X)
  syntax = format("csrs %s, %s", csr.syntax, rs1.syntax)
  image  = format("%32s", csrrs(X(0), csr, rs1).image)
  action = {
    csrrs(X(0), csr, rs1).action;
  }

// Clear bits in CSR
pseudo op csrc(csr: CSR_MODE, rs1: X)
  syntax = format("csrc %s, %s", csr.syntax, rs1.syntax)
  image  = format("%32s", csrrc(X(0), csr, rs1).image)
  action = {
    csrrc(X(0), csr, rs1).action;
  }

// Write CSR, immediate
pseudo op csrwi(csr: CSR_MODE, imm: card(5))
  syntax = format("csrwi %s, 0x%x", csr.syntax, imm)
  image  = format("%32s", csrrwi(X(0), csr, imm).image)
  action = {
    csrrwi(X(0), csr, imm).action;
  }

// Set bits in CSR, immediate
pseudo op csrsi(csr: CSR_MODE, imm: card(5))
  syntax = format("csrsi %s, 0x%x", csr.syntax, imm)
  image  = format("%32s", csrrsi(X(0), csr, imm).image)
  action = {
    csrrsi(X(0), csr, imm).action;
  }

// Clear bits in CSR, immediate
pseudo op csrci(csr: CSR_MODE, imm: card(5))
  syntax = format("csrci %s, 0x%x", csr.syntax, imm)
  image  = format("%32s", csrrci(X(0), csr, imm).image)
  action = {
    csrrci(X(0), csr, imm).action;
  }

// Read FP control/status register
pseudo op frcsr(rd: X)
  syntax = format("frcsr %s", rd.syntax)
  image  = format("%32s", csrrs(rd, FCSR(), X(0)).image)
  action = {
    csrrs(rd, FCSR(), X(0)).action;
  }

// Swap FP control/status register
pseudo op fscsr(rd: X, rs: X)
  syntax = format("fscsr %s, %s", rd.syntax, rs.syntax)
  image  = format("%32s", csrrw(rd, FCSR(), rs).image)
  action = {
    csrrw(rd, FCSR(), rs).action;
  }

// Write FP control/status register
pseudo op fscsr2(rs: X)
  syntax = format("fscsr %s", rs.syntax)
  image  = format("%32s", csrrw(X(0), FCSR(), rs).image)
  action = {
    csrrw(X(0), FCSR(), rs).action;
  }

// Read FP rounding mode
pseudo op frrm(rd: X)
  syntax = format("frrm %s", rd.syntax)
  image  = format("%32s", csrrs(rd, FRM(), X(0)).image)
  action = {
    csrrs(rd, FRM(), X(0)).action;
  }

// Swap FP rounding mode
pseudo op fsrm(rd: X, rs: X)
  syntax = format("fsrm %s, %s", rd.syntax, rs.syntax)
  image  = format("%32s", csrrw(rd, FRM(), rs).image)
  action = {
    csrrw(rd, FRM(), rs).action;
  }

// Write FP rounding mode
pseudo op fsrm2(rs: X)
  syntax = format("fsrm %s", rs.syntax)
  image  = format("%32s", csrrw(X(0), FRM(), rs).image)
  action = {
    csrrw(X(0), FRM(), rs).action;
  }

// Swap FP rounding mode, immediate
pseudo op fsrmi(rd: X, imm: card(5))
  syntax = format("fsrmi %s, 0x%x", rd.syntax, imm)
  image  = format("%32s", csrrwi(rd, FRM(), imm).image)
  action = {
    csrrwi(rd, FRM(), imm).action;
  }

// Write FP rounding mode, immediate
pseudo op fsrmi2(imm: card(5))
  syntax = format("fsrmi 0x%x", imm)
  image  = format("%32s", csrrwi(X(0), FRM(), imm).image)
  action = {
    csrrwi(X(0), FRM(), imm).action;
  }

// Read FP exception flags
pseudo op frflags(rd: X)
  syntax = format("frflags %s", rd.syntax)
  image  = format("%32s", csrrs(rd, FFLAGS(), X(0)).image)
  action = {
    csrrs(rd, FFLAGS(), X(0)).action;
  }

// Swap FP exception flags
pseudo op fsflags(rd: X, rs: X)
  syntax = format("fsflags %s, %s", rd.syntax, rs.syntax)
  image  = format("%32s", csrrw(rd, FFLAGS(), rs).image)
  action = {
    csrrw(rd, FFLAGS(), rs).action;
  }

// Write FP exception flags
pseudo op fsflags2(rs: X)
  syntax = format("fsflags %s", rs.syntax)
  image  = format("%32s", csrrw(X(0), FFLAGS(), rs).image)
  action = {
    csrrw(X(0), FFLAGS(), rs).action;
  }

// Swap FP exception flags, immediate
pseudo op fsflagsi(rd: X, imm: card(5))
  syntax = format("fsflagsi %s, 0x%x", rd.syntax, imm)
  image  = format("%32s", csrrwi(rd, FFLAGS(), imm).image)
  action = {
    csrrwi(rd, FFLAGS(), imm).action;
  }

// Write FP exception flags, immediate
pseudo op fsflagsi2(imm: card(5))
  syntax = format("fsflagsi 0x%x", imm)
  image  = format("%32s", csrrwi(X(0), FFLAGS(), imm).image)
  action = {
    csrrwi(X(0), FFLAGS(), imm).action;
  }

/*
rdinstret[h] rd csrrs rd, instret[h], x0 Read instructions-retired counter
rdcycle[h] rd csrrs rd, cycle[h], x0 Read cycle counter
rdtime[h] rd csrrs rd, time[h], x0 Read real-time clock
*/

op rv32i_pseudo_system = csrw
                       | csrr
                       | csrs
                       | csrc
                       | csrwi
                       | csrsi
                       | csrci
                       | frcsr
                       | fscsr
                       | fscsr2
                       | frrm
                       | fsrm
                       | fsrm2
                       | fsrmi
                       | fsrmi2
                       | frflags
                       | fsflags
                       | fsflags2
                       | fsflagsi
                       | fsflagsi2

// Load address
pseudo op la(rd : X, symbol : WORD)
  syntax = format("la %s, %<label>d", rd.syntax, symbol)
  image  = format("%s%s", auipc(rd, symbol<31..12>).image,
                          addi(rd, rd, symbol<11..0>).image)
  action = {
    auipc(rd, symbol<31..12>).action;
    addi(rd, rd, symbol<11..0>).action;
  }

// Load global
pseudo op lb_global(rd : X, symbol : WORD)
  syntax = format("lb %s, %<label>d", rd.syntax, symbol)
  image  = format("%s%s", auipc(rd, symbol<31..12>).image,
                          lb(rd, rd, symbol<11..0>).image)
  action = {
    auipc(rd, symbol<31..12>).action;
    lb(rd, rd, symbol<11..0>).action;
  }

// Load immediate
pseudo op li(rd : X, symbol : XWORD)
  syntax = format("li %s, %<label>d", rd.syntax, symbol)
  image  = format("%s%s%s%s%s%s%s%s%s%s%s",
#ifdef RV64I
                        ori(rd, X(0), coerce(card(12), symbol<63..53>)).image,
                        slli(rd, rd, 11).image,
                        ori(rd, X(0), coerce(card(12), symbol<52..42>)).image,
                        slli(rd, rd, 11).image,
                        ori(rd, X(0), coerce(card(12), symbol<41..32>)).image,
                        slli(rd, rd, 10).image,
#endif
                        ori(rd, X(0), coerce(card(12), symbol<31..21>)).image,
                        slli(rd, rd, 11).image,
                        ori(rd, X(0), coerce(card(12), symbol<20..10>)).image,
                        slli(rd, rd, 11).image,
                        ori(rd, X(0), coerce(card(12), symbol<9..0>)).image)
  action = {
#ifdef RV64I
    ori(rd, X(0), coerce(card(12), symbol<63..53>)).action;
    slli(rd, rd, 11).action;
    ori(rd, X(0), coerce(card(12), symbol<52..42>)).action;
    slli(rd, rd, 11).action;
    ori(rd, X(0), coerce(card(12), symbol<41..32>)).action;
    slli(rd, rd, 10).action;
#endif
    ori(rd, X(0), coerce(card(12), symbol<31..21>)).action;
    slli(rd, rd, 11).action;
    ori(rd, X(0), coerce(card(12), symbol<20..10>)).action;
    slli(rd, rd, 11).action;
    ori(rd, X(0), coerce(card(12), symbol<9..0>)).action;
  }

//==================================================================================================
// Instruction Groups
//==================================================================================================

op rv32i = lui
         | auipc
         | jal
         | jalr
         | beq
         | bne
         | blt
         | bge
         | bltu
         | bgeu
         | rv32i_mmu // 8
         | addi
         | slti
         | sltiu
         | xori
         | ori
         | andi
         | slli
         | srli
         | srai
         | sll
         | srl
         | sra
         | add
         | sub
         | slt
         | sltu
         | xor
         | or
         | and // 37
         | rv32i_system // 10

         // Pseudoinstructions
         | nop
         | mv
         | not
         | neg
         | seqz
         | snez
         | sltz
         | sgtz
         | j
         | jr
         | beqz
         | bnez
         | blez
         | bgez
         | bltz
         | bgtz // 16
         | rv32i_pseudo_system // 20
         | la
         | lb_global
         | li

//==================================================================================================
// The End
//==================================================================================================

#endif
